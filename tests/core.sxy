
; Sexy core

; def

(def x 5)
(sys.test 'def (= x 5))


; quote

(sys.test 'quote-symbol (= (quote foo) 'foo))
(sys.test 'quote-list (= (list 1 2 3) '(1 2 3)))


; if

(sys.test 'if-true
    (if true
        true
        false))

(sys.test 'if-false
    (if false
        false
        true))

; seq

(sys.test 'seq-1
    (= (seq 1) 1))

(sys.test 'seq-2
    (= (seq 1 2) 2))

(sys.test 'seq-3
    (= (seq 1 2 3) 3))


; set!

(def mute 1)

(sys.test 'set!
    (seq
        (set! mute 2)
        (= mute 2)))

(sys.test 'set-other
    (seq
        (set! mute "I'm a text!")
        (= mute "I'm a text!")))

(sys.test 'set-higher
    (seq
        ((λ ()
            (set! mute 3)))
        (= mute 3)))

; proc

(sys.test 'proc-identity
    (let ()
        (def foo (proc (x) x))
        (= 'zebra (foo 'zebra))))

(sys.test 'proc-add
    (let ()
        (def bar (proc (x y) (+ x y)))
        (= 5 (bar 2 3))))

(sys.test 'proc-recursion
    (let ()
        (def fact
            (proc (n)
                (if (= n 1)
                    1
                    (* n (fact (- n 1))))))
        (= 120 (fact 5))))

(sys.test 'proc-options
    (let ()
        (def zuul
            (proc (x)
                (if opt.keymaster
                    (if opt.gatekeeper
                        (list x opt.keymaster opt.gatekeeper)
                        (list x opt.keymaster))
                    x)))
        (and
            (= (zuul 'gozer) 'gozer)
            (= (zuul 'gozer keymaster: 'louis) '(gozer louis))
            (= (zuul 'gozer keymaster: 'louis gatekeeper: 'dana) '(gozer louis dana)))))

(sys.test 'proc-rest
    (let ()
        (def embiggen
            (proc (xs)
                (if xs.empty?
                    xs
                    (pair (* 10 xs.head) (embiggen xs.tail)))))
        (def various
            (proc (funk)
                (funk rest)))
        (= (various embiggen 1 2 3) '(10 20 30))))

(sys.test 'proc-mutual-recursion
    (let ()
        (def F
            (proc (n)
                (if (= n 0)
                    1
                    (- n (M (F (- n 1)))))))
        (def M
            (proc (n)
                (if (= n 0)
                    0
                    (- n (F (M (- n 1)))))))
        (and (= (F 19) 12) (= (M 19) 12)))) 

(sys.test 'proc-lexical-closure
    (let ()
        (def counter
            (proc (x)
                (proc ()
                    (if opt.inc
                        (set! x (+ x opt.inc))
                        x))))
        (def c1 (counter 0))
        (def c2 (counter 23))
        (c1 inc: 1)
        (c2 inc: 7)
        (and (= (c1) 1)
             (= (c2) 30)
             ((proc ()
                (def x 99)
                (c1 inc: 7)
                (and (= x 99)
                     (= (c1) 8)))))))


; continuations - return, gate, and capture

(sys.test 'return
    (= 10
        ((proc ()
            (return 10)
            20
            30))))

(sys.test 'return-nested
    (= 5
        ((proc ()
            (def x ((proc () (return 10) 20)))
            (if (= x 10)
                (return 5)
                6)))))

(sys.test 'gate-no-capture
    (= 3
        (gate (+ 1 2))))

(sys.test 'gate-abort
    (= 2
        (gate
            (+ 1
                (capture k 2)))))

(sys.test 'capture-call-1
    (= 3
        (gate
            (+ 1
                (capture k (+ 1 (k 1)))))))

(sys.test 'capture-call-n
    (= 5
        (gate
            (+ 1
                (capture k (+ 1 (k (k (k 1)))))))))


; errors - ensure, guard, and error

(proc proc-handler (err kontinue)
    (if (= err 'resume)
        (kontinue 42)
        (if (= err 'default)
            69
            (error 'we-are-fucked))))

(sys.test 'guard-no-error
    (= 6
        (+ 1
            (guard
                proc-handler
                (+ 2 3)))))

(sys.test 'guard-default
    (= 70
        (+ 1
            (guard
                proc-handler
                (+ 2 (error 'default))))))

(sys.test 'guard-continue
    (= 45
        (+ 1
            (guard
                proc-handler
                (+ 2 (error 'resume))))))

(sys.test 'guard-abort
    (= 0
        (guard
            (proc (e k) 0)
            (+ 1
                (guard
                    proc-handler
                    (+ 2 (error 'crap)))))))

(sys.test 'ensure-no-jump
    ((proc ()
        (def x 1)
        (def y
            (ensure
                (proc () (set! x 3))
                (seq (set! x 2) (+ x 3))))
        (and (= x 3) (= y 5)))))

(sys.test 'ensure-no-capture-across
    ((proc ()
        (def x 1)
        (def y
            (gate
                (* 10
                    (ensure
                        (proc () (set! x 3))
                        (seq
                            (set! x 5)
                            (+ 2
                                (capture k (k (k x)))))))))
        (and (= x 3) (= y 90)))))

(sys.test 'ensure-error
    ((proc ()
        (def x 1)
        (def y
            (guard
                (proc (e k) (+ 7 e))
                (ensure
                    (proc () (set! x 3))
                    (seq
                        (set! x 5)
                        (+ 2 (error x))))))
        (and (= x 3) (= y 12)))))
                
(sys.test 'wall
    ((proc ()
        (def x 1)
        (def y 2)
        (def z 3)
        (wall (x y) ; new environment with passed parameters
            (def got-x (= x 1))
            (def got-y (= y 2))
            (def no-z  (not (env.has? 'z)))
            (and got-x got-y no-z)))))

(sys.test 'unicode
    (seq
        (def λא象
            (proc () "λא象"))
        (= (λא象) "λא象")))

; use

(sys.test 'use-proc-file
    (let ()
        (use foreigner "modules/proc.sxy")
        (= (foreigner 1 2) '(foreigner 1 2))))

(sys.test 'use-proc-https
    (let () 
        (use foreigner "https://raw.githubusercontent.com/TurtleKitty/Sexy/master/tests/modules/proc.sxy")
        (= (foreigner 1 2) '(foreigner 1 2))))

(sys.test 'use-parameters-file
    ((proc ()
        (use one   "modules/param.sxy" 1 2)
        (use two   "modules/param.sxy" 'a 'b 'c 'd)
        (use three "modules/param.sxy" 2 3 4 foo: "FOO" bar: "BAR" 5 6)
        (and
            (= one   '(1 2 null null ()))
            (= two   '(a b null null (c d)))
            (= three '(2 3 "FOO" "BAR" (4 5 6)))))))

(sys.test 'use-parameters-https
    ((proc ()
        (use one   "https://raw.githubusercontent.com/TurtleKitty/Sexy/master/tests/modules/param.sxy" 1 2)
        (use two   "https://raw.githubusercontent.com/TurtleKitty/Sexy/master/tests/modules/param.sxy" 'a 'b 'c 'd)
        (use three "https://raw.githubusercontent.com/TurtleKitty/Sexy/master/tests/modules/param.sxy" 2 3 4 foo: "FOO" bar: "BAR" 5 6)
        (and
            (= one   '(1 2 null null ()))
            (= two   '(a b null null (c d)))
            (= three '(2 3 "FOO" "BAR" (4 5 6)))))))

(sys.test 'use-other-file
    (let ()
        (use other "modules/use_other.sxy" foon-type: 'adler)
        (= other '(foonballardy 5 7 adler))))

(sys.test 'use-other-https
    (let ()
        (use other "https://raw.githubusercontent.com/TurtleKitty/Sexy/master/tests/modules/use_other.sxy" foon-type: 'bobkin)
        (= other '(foonballardy 5 7 bobkin))))

(sys.test 'use-macro-file
    ((proc ()
        (load "modules/macro.sxy")
        (and
            (= (quote-xyz 1 2 3) '(1 2 3))
            (= (send (calc-xyz 1 2 3) 'code) '(proc (v) (* v 17)))
            (= ((calc-xyz 1 2 3) 4 ) 68)))))

(sys.test 'use-macro-https
    ((proc ()
        (load "https://raw.githubusercontent.com/TurtleKitty/Sexy/master/tests/modules/macro.sxy")
        (and
            (= (quote-xyz 1 2 3) '(1 2 3))
            (= (send (calc-xyz 1 2 3) 'code) '(proc (v) (* v 17)))
            (= ((calc-xyz 1 2 3) 4 ) 68)))))

(sys.test 'use-recursive
    ((proc ()
        (= 120 (load "modules/recursive.sxy" 5)))))

(sys.test 'use-mutually-recursive
    (let ()
        (and
            (= 'x (load "modules/x.sxy" 4))
            (= 'y (load "modules/x.sxy" 5))
            (= 'y (load "modules/y.sxy" 4))
            (= 'x (load "modules/y.sxy" 5)))))

(sys.test 'use-symbol
    (let ()
        (and
            (= '(test "0.2.0") (load sexy/test/sexy-lib/0))
            (= '(test "0.1.2") (load sexy/test/sexy-lib/0.1))
            (= '(test "0.1.0") (load sexy/test/sexy-lib/0.1.0))
            (= '(test "1.6.4") (load sexy/test/sexy-lib/1))
            (= '(test "1.2.4") (load sexy/test/sexy-lib/1.2.4))
            (= '(test "1.5.9") (load sexy/test/sexy-lib/1.5))
            (= '(test "1.5.7") (load sexy/test/sexy-lib/1.5.7))
            (= '(test "0.2.0") (load github/TurtleKitty/sexy-lib/test/sexy-lib/0))
            (= '(test "1.6.4") (load github/TurtleKitty/sexy-lib/test/sexy-lib/1)))))

