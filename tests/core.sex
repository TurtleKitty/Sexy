
; Sexy core

; def

(def x 5)
(sys.test 'def (= x 5))


; quote

(sys.test 'quote-symbol (= (quote foo) 'foo))
(sys.test 'quote-list (= (list 1 2 3) '(1 2 3)))


; if

(sys.test 'if-true
    (if true
        true
        false))

(sys.test 'if-false
    (if false
        false
        true))

; seq

(sys.test 'seq-1
    (= (seq 1) 1))

(sys.test 'seq-2
    (= (seq 1 2) 2))

(sys.test 'seq-3
    (= (seq 1 2 3) 3))


; set!

(def mute 1)

(sys.test 'set!
    (seq
        (set! mute 2)
        (= mute 2)))

(sys.test 'set-other
    (seq
        (set! mute "I'm a string!")
        (= mute "I'm a string!")))

(sys.test 'set-higher
    (seq
        ((fn ()
            (set! mute 3)))
        (= mute 3)))

; fn

(sys.test 'fn-identity
    (seq
        (def foo (fn (x) x))
        (= 'zebra (foo 'zebra))))

(sys.test 'fn-add
    (seq
        (def bar (fn (x y) (+ x y)))
        (= 5 (bar 2 3))))

(sys.test 'fn-recursion
    (seq
        (def fact
            (fn (n)
                (if (= n 1)
                    1
                    (* n (fact (- n 1))))))
        (= 120 (fact 5))))

(sys.test 'fn-options
    (seq
        (def zuul
            (fn (x)
                (if opt.keymaster
                    (if opt.gatekeeper
                        (list x opt.keymaster opt.gatekeeper)
                        (list x opt.keymaster))
                    x)))
        (and
            (= (zuul 'gozer) 'gozer)
            (= (zuul 'gozer keymaster: 'louis) '(gozer louis))
            (= (zuul 'gozer keymaster: 'louis gatekeeper: 'dana) '(gozer louis dana)))))

(sys.test 'fn-rest
    (seq
        (def embiggen
            (fn (xs)
                (if xs.empty?
                    xs
                    (pair (* 10 xs.head) (embiggen xs.tail)))))
        (def various
            (fn (funk)
                (funk rest)))
        (= (various embiggen 1 2 3) '(10 20 30))))

(sys.test 'fn-mutual-recursion
    (seq
        (def F
            (fn (n)
                (if (= n 0)
                    1
                    (- n (M (F (- n 1)))))))
        (def M
            (fn (n)
                (if (= n 0)
                    0
                    (- n (F (M (- n 1)))))))
        (and (= (F 19) 12) (= (M 19) 12)))) 

(sys.test 'fn-lexical-closure
    (seq
        (def counter
            (fn (x)
                (fn ()
                    (if opt.inc
                        (set! x (+ x opt.inc))
                        x))))
        (def c1 (counter 0))
        (def c2 (counter 23))
        (c1 inc: 1)
        (c2 inc: 7)
        (and (= (c1) 1)
             (= (c2) 30)
             ((fn ()
                (def x 99)
                (c1 inc: 7)
                (and (= x 99)
                     (= (c1) 8)))))))


; continuations - return, gate, and capture

(sys.test 'return
    (= 10
        ((fn ()
            (return 10)
            20
            30))))

(sys.test 'return-nested
    (= 5
        ((fn ()
            (def x ((fn () (return 10) 20)))
            (if (= x 10)
                (return 5)
                6)))))

(sys.test 'gate-no-capture
    (= 3
        (gate (+ 1 2))))

(sys.test 'gate-abort
    (= 2
        (gate
            (+ 1
                (capture (fn (c) 2))))))

(sys.test 'capture-call-1
    (= 3
        (gate
            (+ 1
                (capture (fn (c) (+ 1 (c 1))))))))

(sys.test 'capture-call-n
    (= 5
        (gate
            (+ 1
                (capture (fn (c) (+ 1 (c (c (c 1))))))))))


; errors - ensure, guard, and error

(fun fun-handler (err kontinue)
    (if (= err 'resume)
        (kontinue 42)
        (if (= err 'default)
            69
            (error 'we-are-fucked))))

(sys.test 'guard-no-error
    (= 6
        (+ 1
            (guard
                fun-handler
                (+ 2 3)))))

(sys.test 'guard-default
    (= 70
        (+ 1
            (guard
                fun-handler
                (+ 2 (error 'default))))))

(sys.test 'guard-continue
    (= 45
        (+ 1
            (guard
                fun-handler
                (+ 2 (error 'resume))))))

(sys.test 'guard-abort
    (= 0
        (guard
            (fn (e k) 0)
            (+ 1
                (guard
                    fun-handler
                    (+ 2 (error 'crap)))))))

(sys.test 'ensure-no-jump
    ((fn ()
        (def x 1)
        (def y
            (ensure
                (fn () (set! x 3))
                (seq (set! x 2) (+ x 3))))
        (and (= x 3) (= y 5)))))

(sys.test 'ensure-no-capture-across
    ((fn ()
        (def x 1)
        (def y
            (gate
                (* 10
                    (ensure
                        (fn () (set! x 3))
                        (seq
                            (set! x 5)
                            (+ 2
                                (capture (fn (k) (k (k x))))))))))
        (and (= x 3) (= y 90)))))

(sys.test 'ensure-error
    ((fn ()
        (def x 1)
        (def y
            (guard
                (fn (e k) (+ 7 e))
                (ensure
                    (fn () (set! x 3))
                    (seq
                        (set! x 5)
                        (+ 2 (error x))))))
        (and (= x 3) (= y 12)))))
                
(sys.test 'wall
    ((fn ()
        (def x 1)
        (def y 2)
        (def z 3)
        (wall (x y)
            (def got-x (= x 1))
            (def got-y (= y 2))
            (def no-z  (not (env.has? 'z)))
            (and got-x got-y no-z)))))

; use


(sys.test 'use-file
    (seq
        (use "~/dev/sexy/tests/module.sex")
        (= (foreign-fn 1 2) '(foreigner 1 2))))

(sys.test 'use-http
    (seq 
        (use "http://anwu.org/sexylib/fun.sex/1.0")
        (= (fun-fn 3 4 5) '(fun-object 3 7 20))))

;(sys.test 'use-https
    ;(use "https://raw.githubusercontent.com/TurtleKitty/Sexy/master/tests/pre.sex")


;(sys.test 'use-as
;    (seq
;        (use "~/dev/sexy/tests/module.sex" as: meow)
;        (= (meow.foreign-fn 3 4) '(foreigner 3 4))))


; misc

(sys.test 'unicode
    (seq
        (def λא象
            (fn (x) (* 10 x)))
        (= (λא象 10) 100)))


(sys.test 'tco
    (seq
        (def count 30000)
        (def upto
            (fn (max)
                (def helper
                    (fn (acc)
                        (if (= acc max)
                            acc
                            (helper (+ acc 1)))))
                (helper 0)))
        (= (upto count) count)))

