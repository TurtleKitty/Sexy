
; Sexy core

; def

(def x 5)
(test 'def (= x 5))


; quote

(test 'quote-symbol (= (quote foo) 'foo))
(test 'quote-list (= (list 1 2 3) '(1 2 3)))


; if

(test 'if-true
    (if true
        true
        false))

(test 'if-false
    (if false
        false
        true))

; seq

(test 'seq-1
    (= (seq 1) 1))

(test 'seq-2
    (= (seq 1 2) 2))

(test 'seq-3
    (= (seq 1 2 3) 3))


; set!

(def mute 1)

(test 'set!
    (seq
        (set! mute 2)
        (= mute 2)))

(test 'set-other
    (seq
        (set! mute "I'm a string!")
        (= mute "I'm a string!")))

(test 'set-higher
    (seq
        ((fn ()
            (set! mute 3)))
        (= mute 3)))

; fn

(test 'fn-identity
    (seq
        (def foo (fn (x) x))
        (= 'zebra (foo 'zebra))))

(test 'fn-add
    (seq
        (def bar (fn (x y) (+ x y)))
        (= 5 (bar 2 3))))

(test 'fn-recursion
    (seq
        (def fact
            (fn (n)
                (if (= n 1)
                    1
                    (* n (fact (- n 1))))))
        (= 120 (fact 5))))

(test 'fn-options
    (seq
        (def zuul
            (fn (x)
                (if opt.keymaster
                    (if opt.gatekeeper
                        (list x opt.keymaster opt.gatekeeper)
                        (list x opt.keymaster))
                    x)))
        (and?
            (= (zuul 'gozer) 'gozer)
            (= (zuul 'gozer keymaster: 'louis) '(gozer louis))
            (= (zuul 'gozer keymaster: 'louis gatekeeper: 'dana) '(gozer louis dana)))))

(test 'fn-rest
    (seq
        (def embiggen
            (fn (xs)
                (if xs.empty?
                    xs
                    (pair (* 10 xs.head) (embiggen xs.tail)))))
        (def various
            (fn (funk)
                (funk rest)))
        (= (various embiggen 1 2 3) '(10 20 30))))

(test 'fn-mutual-recursion
    (seq
        (def F
            (fn (n)
                (if (= n 0)
                    1
                    (- n (M (F (- n 1)))))))
        (def M
            (fn (n)
                (if (= n 0)
                    0
                    (- n (F (M (- n 1)))))))
        (and? (= (F 19) 12) (= (M 19) 12)))) 

(test 'fn-lexical-closure
    (seq
        (def counter
            (fn (x)
                (fn ()
                    (if opt.inc
                        (set! x (+ x opt.inc))
                        x))))
        (def c1 (counter 0))
        (def c2 (counter 23))
        (c1 inc: 1)
        (c2 inc: 7)
        (and? (= (c1) 1)
              (= (c2) 30)
              ((fn ()
                (def x 99)
                (c1 inc: 7)
                (and? (= x 99)
                      (= (c1) 8)))))))
            

; continuations - return, gate, and capture

(test 'return
    (= 10
        ((fn ()
            (return 10)
            20
            30))))

(test 'return-nested
    (= 5
        ((fn ()
            (def x ((fn () (return 10) 20)))
            (if (= x 10)
                (return 5)
                6)))))

(test 'gate-no-capture
    (= 3
        (gate (+ 1 2))))

(test 'gate-abort
    (= 2
        (gate
            (+ 1
                (capture (fn (c) 2))))))

(test 'capture-call-1
    (= 3
        (gate
            (+ 1
                (capture (fn (c) (+ 1 (c 1))))))))

(test 'capture-call-n
    (= 5
        (gate
            (+ 1
                (capture (fn (c) (+ 1 (c (c (c 1))))))))))


; errors - ensure, guard, and error

(fun fun-handler (err kontinue)
    (if (= err 'resume)
        (kontinue 42)
        (if (= err 'default)
            69
            (error 'we-are-fucked))))

(test 'guard-no-error
    (= 6
        (+ 1
            (guard
                fun-handler
                (+ 2 3)))))

(test 'guard-default
    (= 70
        (+ 1
            (guard
                fun-handler
                (+ 2 (error 'default))))))

(test 'guard-continue
    (= 45
        (+ 1
            (guard
                fun-handler
                (+ 2 (error 'resume))))))

(test 'guard-abort
    (= 0
        (guard
            (fn (e k) 0)
            (+ 1
                (guard
                    fun-handler
                    (+ 2 (error 'crap)))))))

(test 'ensure-no-jump
    ((fn ()
        (def x 1)
        (def y
            (ensure
                (fn () (set! x 3))
                (seq (set! x 2) (+ x 3))))
        (and? (= x 3) (= y 5)))))

(test 'ensure-no-capture-across
    ((fn ()
        (def x 1)
        (def y
            (gate
                (* 10
                    (ensure
                        (fn () (set! x 3))
                        (seq
                            (set! x 5)
                            (+ 2
                                (capture (fn (k) (k (k x))))))))))
        (and? (= x 3) (= y 90)))))

(test 'ensure-error
    ((fn ()
        (def x 1)
        (def y
            (guard
                (fn (e k) (+ 7 e))
                (ensure
                    (fn () (set! x 3))
                    (seq
                        (set! x 5)
                        (+ 2 (error x))))))
        (and? (= x 3) (= y 12)))))
                


; use


(test 'use-file
    (seq
        (use "~/dev/sexy/tests/module.sex")
        (= (foreign-fn 1 2) '(foreigner 1 2))))

(test 'use-http
    (seq 
        (use "http://anwu.org/sexylib/fun.sex/1.0")
        (= (fun-fn 3 4 5) '(fun-object 3 7 20))))

;(test 'use-https
    ;(use "https://raw.githubusercontent.com/TurtleKitty/Sexy/master/tests/pre.sex")


(test 'use-as
    (seq
        (use "~/dev/sexy/tests/module.sex" as: meow)
        (= (meow.foreign-fn 3 4) '(foreigner 3 4))))


; misc

(test 'unicode
    (seq
        (def λא象
            (fn (x) (* 10 x)))
        (= (λא象 10) 100)))


(test 'tco
    (seq
        (def count 120000)
        (def upto
            (fn (max)
                (def helper
                    (fn (acc)
                        (if (= acc max)
                            acc
                            (helper (+ acc 1)))))
                (helper 0)))
        (= (upto count) count)))

